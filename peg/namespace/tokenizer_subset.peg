
{
    package namespace
}
    

// -------------------------------------------------------------------------
//  Namespace
// -------------------------------------------------------------------------

FILE = STATEMENTS _ EOF

STATEMENTS = statements:(BLOCK_STATEMENT/ CREATE_OBJECT / CREATE_CLASS)*

CREATE_OBJECT = _ CREATE_NAMESPACE_OBJECT? _ NAMESPACE? _ ";"

BLOCK_STATEMENT = _ NAMESPACE _ ":" _ "{" STATEMENTS _ "}"

CREATE_NAMESPACE_OBJECT = "create"i _ typ:NAMESPACE_OBJECT_TYPE _ name:QUOTED_NAME

NAMESPACE_OBJECT_TYPE = typ:("database" / "domain" / "context" / "aggregate")

NAMESPACE = (USING_DATABASE / FOR_DOMAIN / IN_CONTEXT / WITHIN_AGGREGATE)*

USING_DATABASE = _ "using"i _ "database"i _ name:QUOTED_NAME

FOR_DOMAIN = _ "for"i _ "domain"i _ name:QUOTED_NAME

IN_CONTEXT = _ "in"i _ "context"i _ name:QUOTED_NAME

WITHIN_AGGREGATE = _ "within"i _ "aggregate"i _ name:QUOTED_NAME

CREATE_CLASS = _ CLASS_OPEN (CREATE_VALUE / CREATE_COMMAND / CREATE_PROJECTION / CREATE_INVARIANT / CREATE_QUERY) CLASS_CLOSE

CREATE_VALUE = _ ("value"i / "entity"i / "event"i)  _ name:QUOTED_NAME _ NAMESPACE _ VALUE_BODY _

CREATE_COMMAND = _ "command"i  _ QUOTED_NAME _ NAMESPACE _ COMMAND_BODY _

CREATE_PROJECTION = _ ("aggregate"i / "domain"i) _ "projection"i  _ QUOTED_NAME _ NAMESPACE _ PROJECTION_BODY _

CREATE_INVARIANT = _  "invariant"i  _ QUOTED_NAME _ "on"i _ CLASS_REF_QUOTES _ NAMESPACE _ INVARIANT_BODY _

CREATE_QUERY = _  "query"i  _ QUOTED_NAME _ "on"i _ CLASS_REF_QUOTES _ NAMESPACE _ QUERY_BODY _

CLASS_OPEN = "<|"

CLASS_CLOSE = "|>"

// -------------------------------------------------------------------------
//  Class components
// -------------------------------------------------------------------------

CLASS_COMPONENT_TEST = (WHEN / COMMAND_HANDLER / PROPERTIES / CHECK / FUNCTION)* EOF

QUERY_BODY = (PROPERTIES / QUERY_HANDLER)*

INVARIANT_BODY = (PROPERTIES / CHECK)*

PROJECTION_BODY = (WHEN / PROPERTIES / CHECK / FUNCTION)*

WHEN = _ "when"i _ "event"i _ QUOTED_NAME _ "{" _ STATEMENT_BLOCK? _ "}" _

COMMAND_BODY = (COMMAND_HANDLER / PROPERTIES / CHECK / FUNCTION)*

COMMAND_HANDLER = _ "handler"i _ "{" _ COMMAND_STATEMENT_BLOCK? _ "}" _

QUERY_HANDLER = _ "handler"i _ "{" _ STATEMENT_BLOCK? _ "}" _

COMMAND_STATEMENT_BLOCK = _ (COMMAND_STATEMENT)+ _

COMMAND_STATEMENT = STATEMENT / ASSERT / APPLY

ASSERT = _ "assert"i _ "invariant"i _ ("not"i)? _ QUOTED_NAME _ ARGUMENTS? SEMI

APPLY = _ "apply"i _ "event"i _ QUOTED_NAME _ ARGUMENTS? SEMI

VALUE_BODY = (VALUE_COMPONENTS)*

VALUE_COMPONENTS = PROPERTIES / CHECK / FUNCTION

PROPERTIES = _ "properties"i _ "{" PROPERTY_LIST "}" _

PROPERTY_LIST = (PROPERTY)*

PROPERTY = _ TYPE _ IDENTIFIER _  ("=" _ EXPRESSION _)? ";" _

CHECK = _ "check"i _ "(" STATEMENT_BLOCK? ")" _

FUNCTION = _ "function"i _ IDENTIFIER _ PARAMETERS  _ "{" _ STATEMENT_BLOCK? _ "}" _

PARAMETERS = "(" PARAMETER_LIST ")"

PARAMETER_LIST = _ (PARAMETER "," _ )* PARAMETER? _

PARAMETER = _ CLASS_REF _ IDENTIFIER _
// -------------------------------------------------------------------------
//  Statements
// -------------------------------------------------------------------------

STATEMENT_BLOCK = _ (STATEMENT)+ _

STATEMENT = RETURN SEMI / IF / FOREACH / EXPRESSION SEMI

IF = "if" _ EXPRESSION _ "{" _ STATEMENT_BLOCK? "}" _ ("else" _ "{" _ STATEMENT_BLOCK? "}" _)?

FOREACH = "foreach" _ "(" _ EXPRESSION _ "as" (_ IDENTIFIER _ "=>")? _ IDENTIFIER _ ")" _ "{" _ STATEMENT_BLOCK? "}"

RETURN = "return" _ EXPRESSION

// -------------------------------------------------------------------------
//  Expressions
// -------------------------------------------------------------------------

EXRESSION_TEST = EXPRESSION EOF

EXPRESSION = QUERY / ARITHMETIC / COMPARISON / ASSIGNMENT/ LOGICAL / ATOMIC

ATOMIC = PARENTHESIS / NEW / METHODCALL / OBJECTACCESS / ARRAY / LITERAL / UNARY

LITERAL = STRING / FLOAT / BOOLEAN / NULL / INT

NEW = CLASS_REF_QUOTES _ ARGUMENTS?

BOOLEAN  = "true"/"false"

NULL = "null"

ARRAY = _ "[" ARGUMENTLIST? "]" _

STRING = "\"" [a-zA-Z0-9]* "\""

INT = [0-9]+

FLOAT = [0-9]+ [.] [0-9]+

PARENTHESIS = "(" _ EXPRESSION _ ")"

UNARY = INCREMENT / DECREMENT / NEGATE / NOT / POSITIVE

INCREMENT = OBJECTACCESS "++"

DECREMENT = OBJECTACCESS "--"

NEGATE = "-" OBJECTACCESS

NOT = "!" OBJECTACCESS

POSITIVE = "+" OBJECTACCESS

ARITHMETIC = ATOMIC _ OPERATOR _ EXPRESSION

OPERATOR = "+" / "-" / "/" / "*" / "%"

ASSIGNMENT = OBJECTACCESS _ "=" _ EXPRESSION

LOGICAL = IDENTIFIER _ ("and" / "or") _ ATOMIC

COMPARISON = ATOMIC _ ("===" / "!==" / "==" / "!=" / "<=" / ">=" / "<" / ">") _ EXPRESSION

QUERY = "run"i _ "query"i _ QUOTED_NAME _ ARGUMENTS?

OBJECTACCESS = ((METHODCALL / IDENTIFIER) "->")* (METHODCALL / IDENTIFIER)

METHODCALL = IDENTIFIER ARGUMENTS

ARGUMENTS = "(" ARGUMENTLIST? ")"

ARGUMENTLIST =  _ (_ EXPRESSION _ "," _)* EXPRESSION _


// -------------------------------------------------------------------------
//  Core
// -------------------------------------------------------------------------

CLASS_REF_QUOTES = _ "'" CLASS_REF "'"

CLASS_REF = _ CLASS_TYPE "\\" CLASS_NAME

CLASS_TYPE = _ ("value" / "entity" / "command" / "event" / "projection" / "invariant" / "query")

CLASS_NAME = [a-zA-Z]+ [a-zA-Z0-9_-]* {
    return string(c.text), nil;
}

QUOTED_NAME = "'" name:CLASS_NAME "'" {
    return name, nil;
}

TYPE = CLASS_REF / VALUE_TYPE

VALUE_TYPE = _ ("string" / "boolean" / "float" / "map" / "index")

CLASS_IMPLIED_REF = _ "'" CLASS_NAME "'"

IDENTIFIER = [a-zA-Z]+ [a-zA-Z0-9_]*

_ = ( WHITESPACE / EOL )*

SEMI = _ ";" _

WHITESPACE = [ \t\r]

EOL = '\n'

EOF = !.